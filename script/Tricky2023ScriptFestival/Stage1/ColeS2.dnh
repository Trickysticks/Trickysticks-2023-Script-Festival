#TouhouDanmakufu
#ScriptVersion[3]
#System["./../tricky_system/TrickySystem.dnh"]
#Title["Cole Spell 2"]
#Background["./NamelessPath.dnh"]
#Player["./../lib/TrickyPlayer/Tricky.dnh"]
//#BGM["./../sfx/bgm/bgm2.ogg"]

#include "./../lib/TerraShot1x/shot_const.dnh"
#include "./../lib/includes/BossIncludes.dnh"

@Event{
	alternative(GetEventType())
		case(EV_REQUEST_LIFE){ SetScriptResult(1000); }
		case(EV_REQUEST_TIMER){ SetScriptResult(999); }
		case(EV_REQUEST_SPELL_SCORE){ SetScriptResult(10000000); }
		case(EV_GAIN_SPELL){ AddScore(ObjEnemyBossScene_GetInfo(objScene, INFO_SPELL_SCORE)); }
}

@Initialize{

	objEnemy = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objEnemy);
	Invuln(objEnemy, 360, 10, 15);
	PlayEnemyHitSound(objEnemy, 1);
	
	let Cole = GetCurrentScriptDirectory~"sprites/Cole_Sheet.png";
	ObjPrim_SetTexture(objEnemy, Cole); 
	ObjSprite2D_SetSourceRect(objEnemy, 0, 0, 128, 128);
	ascent(x in 0..4){ ObjAnim_AddFrameA1(objEnemy, ANIM_IDLE, 0, 128*x, 128, 128*x+128);}
	descent(x in 0..4){ ObjAnim_AddFrameA1(objEnemy, ANIM_IDLE, 0, 128*x, 128, 128*x+128);}
	ascent(x in 0..4){ObjAnim_AddFrameA1(objEnemy, ANIM_MOVE_RIGHT, 128, 128*x, 128*2, 128*x+128);}
	ObjAnim_AddFrameA1(objEnemy, ANIM_MOVEEND_RIGHT, 128, 128*3, 128*2, 128*4);
	ObjAnim_AddFrameA1(objEnemy, ANIM_MOVEEND_RIGHT, 128, 128*2, 128*2, 128*3);
	ObjAnim_AddFrameA1(objEnemy, ANIM_MOVEEND_RIGHT, 128, 128*1, 128*2, 128*2);
	ascent(x in 0..4){ObjAnim_AddFrameA1(objEnemy, ANIM_MOVE_LEFT, 128*2, 128*x, 128*3, 128*x+128);}
	ObjAnim_AddFrameA1(objEnemy, ANIM_MOVEEND_LEFT, 128*2, 128*3, 128*3, 128*4);
	ObjAnim_AddFrameA1(objEnemy, ANIM_MOVEEND_LEFT, 128*2, 128*2, 128*3, 128*3);
	ObjAnim_AddFrameA1(objEnemy, ANIM_MOVEEND_LEFT, 128*2, 128*1, 128*3, 128*2);
	ascent(x in 0..3){ObjAnim_AddFrameA1(objEnemy, ANIM_ATTACK, 128*4, 128*x, 128*5, 128*x+128);}
	descent(x in 0..3){ObjAnim_AddFrameA1(objEnemy, ANIM_ATTACKEND, 128*4, 128*x, 128*5, 128*x+128);}

	ObjAnim_SetSpeedA1(objEnemy, ANIM_IDLE, 8);
	ObjAnim_SetSpeedA1(objEnemy, ANIM_MOVE_LEFT, 6);
	ObjAnim_SetSpeedA1(objEnemy, ANIM_MOVE_RIGHT, 6);
	ObjAnim_SetSpeedA1(objEnemy, ANIM_ATTACK, 5);
	ObjAnim_SetLoopPointA1(objEnemy, ANIM_MOVE_RIGHT, 3);
	ObjAnim_SetLoopPointA1(objEnemy, ANIM_MOVE_LEFT, 3);
	ObjAnim_SetLoopPointA1(objEnemy, ANIM_ATTACK, 2);
	ObjAnim_StartAnimationA2(objEnemy, 0, 0);
	ObjSprite2D_SetDestCenter(objEnemy); 
	ObjRender_SetScaleXYZ(objEnemy, 0.75, 0.75, 1);
	
	SetAutoDeleteObject(true);
	delayedstart;
	End;
}

task End {
	let itemx = 0;
	let itemy = 0;
	while(ObjEnemy_GetInfo(objEnemy, INFO_LIFE) > 0) { yield; }
	
	SetShotAutoDeleteClip(32, 40, 32, 32);
	NotifyEventAll(EV_SPELLBGEND, 1);
	//DeleteShotWave(1,GetX,GetY,20);
	DeleteShotAll(TYPE_ALL,TYPE_ITEM);
	PlaySoundEffect("end",60,GetCenterX);
	CreateItems02(12, ITEM_POINT_S, GetEnemyX, GetEnemyY, 0, -50, 35, 35, 10000);
	SetForbidPlayerShot(false);
	SetPlayerSpeed(4.2, 1.7);
	SetCommonData("charm", false);
	Obj_Delete(objEnemy);
	CloseScript(GetOwnScriptID);
}

task delayedstart{
	
	ObjMove_SetDestAtFrame(objEnemy, GetCenterX, 120, 60);
	wait(60);
	
	ObjEnemyBossScene_StartSpell(objScene);
	ObjEnemyBossScene_SetSpellTimer(objScene, 60*(60));
	PlaySoundEffect("cardcall", 90, GetCenterX);
	
	let cole_cut = GetCurrentScriptDirectory~"sprites/Cole_Cutin.png";
	ObjCutin_SetSpellcardS4("Charming Demon &quot;Marin Karinon&quot;", cole_cut, "NAZRIN", "255", "0", "128");
	ObjCutin_LaunchS3("NAZRIN", "0", "Hard");
	NotifyEventAll(EV_SPELLBG, 1);
	
	wait(120);
	attack1;
	//attack2;
	SetShotAutoDeleteClip(32, 40, 32, 16);
}

task attack1{
	let x = -20;
	let repeats = 8;
	let offset = 0;
	let one = 1;
	let count = 0;
	let startPoint = [-20, 404];
	TCharge01(GetEnemyX, GetEnemyY, 3, 105, 255, 0, 128, BLEND_ADD_ARGB, 25);
	//ObjAnim_SetAttackA1(objEnemy, true);
	PlaySoundEffect("charge", 100, GetEnemyX);
	PathDisplay(105+120);
	wait(105);
	//ObjAnim_SetAttackA1(objEnemy, false);
	PlaySoundEffect("se_phantom1", 100, GetEnemyX);
	PlaySoundEffect("slash", 100, GetEnemyX);
	PlaySoundEffect("coleLaugh", 100, GetEnemyX);
	CharmEffect(GetEnemyX, GetEnemyY);
	Charm(120);
	wait(150);

	loop{
		//SetIntersectionVisualization(true);
		Spiral1(one, angle);
		wait(180);
		TCharge01(GetEnemyX, GetEnemyY, 3, 105, 255, 0, 128, BLEND_ADD_ARGB, 25);
		//ObjAnim_SetAttackA1(objEnemy, true);
		PlaySoundEffect("charge", 100, GetEnemyX);
		PathDisplay(105+120);
		wait(105);
		//ObjAnim_SetAttackA1(objEnemy, false);
		PlaySoundEffect("se_phantom1", 100, GetEnemyX);
		PlaySoundEffect("slash", 100, GetEnemyX);
		PlaySoundEffect("coleLaugh", 100, GetEnemyX);
		CharmEffect(GetEnemyX, GetEnemyY);
		Charm(120);
		one*=-1;
		wait(180);
		angle+=rand(25, 35);
		//filler time for second wave
		wait(200);
		loop(55){ DeleteShotInCircle(TYPE_SHOT, TYPE_FADE, GetEnemyX, GetEnemyY, 10); yield; }
		TCharge01(GetEnemyX, GetEnemyY, 3, 105, 255, 0, 128, BLEND_ADD_ARGB, 25);
		//ObjAnim_SetAttackA1(objEnemy, true);
		PlaySoundEffect("charge", 100, GetEnemyX);
		PathDisplay(105+120);
		loop(105){ DeleteShotInCircle(TYPE_SHOT, TYPE_FADE, GetEnemyX, GetEnemyY, 10); yield; }
		//ObjAnim_SetAttackA1(objEnemy, false);
		PlaySoundEffect("se_phantom1", 100, GetEnemyX);
		PlaySoundEffect("slash", 100, GetEnemyX);
		PlaySoundEffect("coleLaugh", 100, GetEnemyX);
		CharmEffect(GetEnemyX, GetEnemyY);
		Charm(120);
		one*=-1;
		wait(180);
		//angle+=rand(25, 35);
		
	}	
}

task PathDisplay(frames){
	let laserLength = 0;
	let laserEndLength = 90;
	let laserAngle = 0;
	let time = 0;
	let obj = CreateStraightLaserA1(GetPlayerX, GetPlayerY, 90, 0, 60, frames, SHOT_LASER_GREY, frames);
	let obj2 = CreateStraightLaserA1(GetPlayerX, GetPlayerY, 90, 0, 60, frames, SHOT_LASER_GREY, frames);
	let obj3 = CreateStraightLaserA1(GetPlayerX, GetPlayerY, 90, 0, 60, frames, SHOT_LASER_GREY, frames);
	ObjStLaser_SetPermitExpand(obj, false);
	ObjStLaser_SetPermitExpand(obj2, false);
	ObjStLaser_SetPermitExpand(obj3, false);
	loop(frames){
		laserAngle = AngleBetweenPoints(GetPlayerX, GetPlayerY, GetEnemyX, GetEnemyY);
		if(time<106){
			ObjMove_SetPosition(obj, GetPlayerX, GetPlayerY);
			ObjMove_SetPosition(obj2, GetPlayerX+laserLength*cos(laserAngle), GetPlayerY+laserLength*sin(laserAngle));
			ObjMove_SetPosition(obj3, GetPlayerX+laserLength*cos(laserAngle), GetPlayerY+laserLength*sin(laserAngle));
			ObjStLaser_SetAngle(obj2, laserAngle+180+45);
			ObjStLaser_SetAngle(obj3, laserAngle+180-45);
		}
		ObjStLaser_SetAngle(obj, AngleBetweenPoints(GetPlayerX, GetPlayerY, GetEnemyX, GetEnemyY));
		ObjLaser_SetLength(obj, laserLength);
		ObjLaser_SetLength(obj2, laserLength/2);
		ObjLaser_SetLength(obj3, laserLength/2);
		if(laserLength<laserEndLength){ laserLength+=laserEndLength/90; }
		yield;
		time++;
	}
	Obj_Delete(obj);
	Obj_Delete(obj2);
	Obj_Delete(obj3);
}


task Spiral1(direction, firingDirection){
	let dir = firingDirection;
	let type = 0;
	let type2 = 0;
	ascent(i in 0..40){
		PlaySoundEffect("shot5", 60, GetEnemyX);
		loop(5){
			let obj = CreateDNHShotA2(GetEnemyX, GetEnemyY, 3.7666667-(i/15), dir, -0.0125, 0, SHOT_LARGE_PINK, 10);
			ManageCharmedBullets(obj, type2%2);
			dir+=360/5;
			type+=0.5;
			if(type%2==0){ type2++; }
		}
		
		wait(5);
		dir+=(21*direction)+rand(0.25, 0.25);
	}
}

task ManageCharmedBullets(obj, type){
	let changed = false;
	let time = 0;
	let scale = 1;
	let shotAngle = ObjMove_GetAngle(obj);
	let color = 0;
	while(!GetCommonData("charm", false)){ yield; }
	while(GetCommonData("charm", false)){ 
		if(changed==false){
			if(type==0){ ObjShot_SetGraphic(obj, SHOT_HEART_PINK); changed = true; }
		}
		yield;
	}
	if(!Obj_IsDeleted(obj)){
		if(type==0){ ObjMove_SetAcceleration(obj, 0.075); ObjMove_SetMaxSpeed(obj, 4); }
		else{
			wait(45);
			loop(3){
				if(!Obj_IsDeleted(obj)){ ObjShot_SetGraphic(obj, SHOT_LARGE_RED); }
				wait(10);
				if(!Obj_IsDeleted(obj)){ ObjShot_SetGraphic(obj, SHOT_LARGE_ORANGE); }
				wait(10);
			}
			loop(20){
				time++;
				scale-=1/20;
				ObjRender_SetScaleXYZ(obj, scale);
				ObjShot_SetIntersectionScaleXY(obj, scale, scale);
				if(time%10==0){
					PlaySoundEffect("shot5", 70, GetEnemyX);
					loop(3){
						let obj2 = CreateDNHShotA2(ObjMove_GetX(obj), ObjMove_GetY(obj), 2.5, shotAngle, -0.15, 0.75, [SHOT_RICE_RED, SHOT_RICE_ORANGE][color%2], 15);
						ManageExplosionBullets(obj2);
						shotAngle+=360/3;
					}
					color++;
					shotAngle+=360/3/2;
				}
				yield;
			}
		}
	}
}

task ManageExplosionBullets(obj){
	wait(420);
	if(!Obj_IsDeleted(obj)){
		ObjMove_SetAcceleration(obj, 0.01);
		ObjMove_SetMaxSpeed(obj, 2);
	}
}


task Charm(frames){ //change this to set player speed to 0?
	let magnitude = 1.15;
	SetCommonData("charm", true);
	SetPlayerSpeed(0, 0);
	SetForbidPlayerShot(true);
		ascent(i in 0..frames){
			ObjMove_SetPosition(GetPlayerObjectID, GetPlayerX-(magnitude*(sin(i*(180/frames)))*cos(GetAngleToPlayer(objEnemy))), GetPlayerY-(magnitude*(sin(i*(180/frames)))*sin(GetAngleToPlayer(objEnemy)))); //non-linear movement
			//ObjMove_SetPosition(GetPlayerObjectID, GetPlayerX-magnitude*cos(GetAngleToPlayer(objEnemyMain)), GetPlayerY-magnitude*sin(GetAngleToPlayer(objEnemyMain))); //linear movement
			yield;
		}
	SetForbidPlayerShot(false);
	SetPlayerSpeed(4.2, 1.7);
	SetCommonData("charm", false);
	PlaySoundEffect("ping", 90, GetCenterX);
	}

task CharmEffect(x,y) {
	let obj1 = ObjPrim_Create(OBJ_SPRITE_2D);
	Obj_SetRenderPriorityI(obj1,40);
	ObjRender_SetBlendType(obj1,BLEND_ADD_RGB);
	ObjPrim_SetTexture(obj1, GetCurrentScriptDirectory~"sprites/Heart.png");
	ObjSprite2D_SetSourceRect(obj1,0,0,256,256);
	ObjSprite2D_SetDestCenter(obj1);

	let obj2 = ObjPrim_Create(OBJ_SPRITE_2D);
	Obj_SetRenderPriorityI(obj2,40);
	ObjRender_SetBlendType(obj2,BLEND_ADD_RGB);
	ObjPrim_SetTexture(obj2,GetCurrentScriptDirectory~"sprites/ConcentrationCircle.png");
	ObjSprite2D_SetSourceRect(obj2,0,0,256,256);
	ObjSprite2D_SetDestCenter(obj2);
	
	let maxscale1 = 2.25;
	let maxscale2 = 3.375;
	let scale1 = 0;
	let scale2 = 0;
	let alpha = 255*2;

	ObjRender_SetPosition(obj1,x,y+10,0);
	ObjRender_SetPosition(obj2,x,y,0);

	ascent(i in 0..60) {
		scale1 = maxscale1*sin(90*i/60);
		scale2 = maxscale2*sin(90*i/60);
		alpha-=(255*2)/60;
		ObjRender_SetScaleXYZ(obj1,scale1,scale1,scale1);
		ObjRender_SetColor(obj1,alpha,0,alpha);
		ObjRender_SetScaleXYZ(obj2,scale2,scale2,scale2);
		ObjRender_SetColor(obj2,alpha,0,alpha);
		yield;
	}
}