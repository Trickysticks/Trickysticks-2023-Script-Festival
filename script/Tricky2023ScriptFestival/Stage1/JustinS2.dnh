#TouhouDanmakufu
#ScriptVersion[3]
#System["./../tricky_system/TrickySystem.dnh"]
#Title["Justin Spell 2"]
#Background["./NamelessPath.dnh"]
#Player["./../lib/ColePlayer/Cole.dnh"]
//#BGM["./../sfx/bgm/bgm2.ogg"]

#include "./../lib/TerraShot1x/shot_const.dnh"
#include "./../lib/includes/BossIncludes.dnh"

@Event{
	alternative(GetEventType())
		case(EV_REQUEST_LIFE){ SetScriptResult(1000); }
		case(EV_REQUEST_TIMER){ SetScriptResult(999); }
		case(EV_REQUEST_SPELL_SCORE){ SetScriptResult(10000000); }
		//case(EV_REQUEST_IS_DURABLE_SPELL){ SetScriptResult(true); }
		case(EV_GAIN_SPELL){ AddScore(ObjEnemyBossScene_GetInfo(objScene, INFO_SPELL_SCORE)); }
}

@Initialize{

	objEnemy = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objEnemy);
	Invuln(objEnemy, 360, 13.5, 10);
	PlayEnemyHitSound(objEnemy, 1);
	
	let Justin = GetCurrentScriptDirectory~"sprites/Justin_Sheet.png";
	ObjPrim_SetTexture(objEnemy, Justin); 
	ObjSprite2D_SetSourceRect(objEnemy, 0, 0, 96, 96);
	ascent(x in 0..4){ ObjAnim_AddFrameA1(objEnemy, ANIM_IDLE, 0, 96*x, 96, 96*x+96);}
	descent(x in 0..4){ ObjAnim_AddFrameA1(objEnemy, ANIM_IDLE, 0, 96*x, 96, 96*x+96);}
	ascent(x in 0..4){ObjAnim_AddFrameA1(objEnemy, ANIM_MOVE_RIGHT, 96, 96*x, 96*2, 96*x+96);}
	ObjAnim_AddFrameA1(objEnemy, ANIM_MOVEEND_RIGHT, 96, 96*3, 96*2, 96*4);
	ObjAnim_AddFrameA1(objEnemy, ANIM_MOVEEND_RIGHT, 96, 96*2, 96*2, 96*3);
	ObjAnim_AddFrameA1(objEnemy, ANIM_MOVEEND_RIGHT, 96, 96*1, 96*2, 96*2);
	ascent(x in 0..4){ObjAnim_AddFrameA1(objEnemy, ANIM_MOVE_LEFT, 96*2, 96*x, 96*3, 96*x+96);}
	ObjAnim_AddFrameA1(objEnemy, ANIM_MOVEEND_LEFT, 96*2, 96*3, 96*3, 96*4);
	ObjAnim_AddFrameA1(objEnemy, ANIM_MOVEEND_LEFT, 96*2, 96*2, 96*3, 96*3);
	ObjAnim_AddFrameA1(objEnemy, ANIM_MOVEEND_LEFT, 96*2, 96*1, 96*3, 96*2);
	ascent(x in 0..3){ObjAnim_AddFrameA1(objEnemy, ANIM_ATTACK, 96*4, 96*x, 96*5, 96*x+96);}
	descent(x in 0..3){ObjAnim_AddFrameA1(objEnemy, ANIM_ATTACKEND, 96*4, 96*x, 96*5, 96*x+96);}
	ObjAnim_SetSpeedA1(objEnemy, ANIM_IDLE, 8);
	ObjAnim_SetSpeedA1(objEnemy, ANIM_MOVE_LEFT, 6);
	ObjAnim_SetSpeedA1(objEnemy, ANIM_MOVE_RIGHT, 6);
	ObjAnim_SetSpeedA1(objEnemy, ANIM_ATTACK, 5);
	ObjAnim_SetLoopPointA1(objEnemy, ANIM_MOVE_RIGHT, 3);
	ObjAnim_SetLoopPointA1(objEnemy, ANIM_MOVE_LEFT, 3);
	ObjAnim_SetLoopPointA1(objEnemy, ANIM_ATTACK, 2);
	ObjAnim_StartAnimationA2(objEnemy, 0, 0);
	ObjSprite2D_SetDestCenter(objEnemy); 
	ObjRender_SetScaleXYZ(objEnemy, 1, 1, 1);
	
	SetAutoDeleteObject(true);
	delayedstart;
	End;
}

task End {
	let itemx = 0;
	let itemy = 0;
	while(ObjEnemy_GetInfo(objEnemy, INFO_LIFE) > 0) { yield; }
	
	SetShotAutoDeleteClip(32, 40, 32, 32);
	NotifyEventAll(EV_SPELLBGEND, 0);
	//DeleteShotWave(1,GetX,GetY,20);
	DeleteShotAll(TYPE_ALL,TYPE_ITEM);
	PlaySoundEffect("end",60,GetCenterX);
	CreateItems02(12, ITEM_POINT_S, GetEnemyX, GetEnemyY, 0, -50, 35, 35, 10000);
	SetForbidPlayerShot(false);
	//SetPlayerSpeed(4.2, 1.7);
	SetCommonData("charm", false);
	Obj_Delete(objEnemy);
	CloseScript(GetOwnScriptID);
}

task delayedstart{
	
	ObjMove_SetDestAtFrame(objEnemy, GetCenterX, 120, 60, LERP_DECELERATE);
	wait(60);
	
	ObjEnemyBossScene_StartSpell(objScene);
	ObjEnemyBossScene_SetSpellTimer(objScene, (75)*(60));
	PlaySoundEffect("cardcall", 90, GetCenterX);
	
	let justin_cut = GetCurrentScriptDirectory~"sprites/JustinCutin.png";
	ObjCutin_SetSpellcardS4("Offense &quot;Carnificatorâ€™s Frenzy&quot;", justin_cut, "NAZRIN", "255", "15", "34");
	ObjCutin_LaunchS3("NAZRIN", "0", "Hard");
	//HiroyukiCutin;
	NotifyEventAll(EV_SPELLBG, 0);
	
	wait(120);
	attack1;
	//attack2;
}


task attack1{
	let one = 1;
	let dir = 90;
	SetShotAutoDeleteClip(64, 64, 64, 64);
	
	loop{
		TCharge01(GetEnemyX, GetEnemyY, 3, 120, 255, 0, 0, BLEND_ADD_ARGB, 25);
		PlaySoundEffect("charge", 100, GetEnemyX);
		wait(120);
		loop(4){
			PlaySoundEffect("slash", 100, GetEnemyX);
			Slash(one);
			one*=-1;
			wait(40);
		}
		PlaySoundEffect("slash", 100, GetEnemyX);
		Slash(one);
		Slash(one*-1);
		wait(30);
		ExMove(objEnemy, GetCenterX+rand(-0.1, 0.1), rand(25, 35), 120+rand(-5, 5), 20, 1, 18);
		ascent(i in 0..3){
			loop(8){
				CShot(GetEnemyX, GetEnemyY, 4, dir, -0.05, 1.5, SHOT_BUBBLE_YELLOW, 15);
				dir+=360/8;
			}
			dir+=360/8/2.25;
			PlaySoundEffect("shot", 80, GetEnemyX);
			wait(30);
		}
		one*=-1;
		wait(30);
	}	
}

task Slash(dir){
	let obj = CreateShotA1(GetEnemyX+35*dir, GetEnemyY-50, 0, 90, SHOT_BALL_RED, 0);
	ObjShot_SetSpellResist(obj, true);
	ObjShot_SetAutoDelete(obj, false);
	Obj_SetVisible(obj, false);
	ObjShot_SetIntersectionEnable(obj, false);
	
	let spd = 4;
	let angle = 35;
	let shotangle = 0;
	let shotangle2 = 0;
	if(dir == -1){ angle = 180-35; }
	let time = 0;
	
	ObjMove_SetSpeed(obj, 4);
	ObjMove_SetAcceleration(obj, 0.05);
	ObjMove_SetMaxSpeed(obj, 8);
	
	while(!Obj_IsDeleted(obj) && time<70){
		time++;
		if(time<20){
			angle+=6.5*dir;
			shotangle2 = angle;
		}
		ObjMove_SetAngle(obj, angle);
		
		shotangle = angle;
		if(dir==-1){ shotangle-=15; }
		if(time%5==0){
			PlaySoundEffect("shot2", 80, ObjMove_GetX(obj));
			loop(5){
				let obj1 = CShot(ObjMove_GetX(obj), ObjMove_GetY(obj), 2, shotangle+45+rand(-2.5, 2.5), -0.2, 0, SHOT_SCALE_ORANGE, 2);
				ObjMove_AddPatternA2(obj1, 60, 0, NO_CHANGE, 0.025, 1.55+rand(-0.05, 0.05), 0);
				shotangle+=360/5;
			}
		}
		
		if(time%6==0){
			loop(7){
				let obj1 = CShot(ObjMove_GetX(obj), ObjMove_GetY(obj), 6, shotangle2, -0.5, 2.0, SHOT_ICE_RED, 30);
				shotangle2+=360/7;
			}
			shotangle2+=360/7/3*dir;
		}
		yield;
	}
	
	//PlaySoundEffect("shot", 90, ObjMove_GetX(obj));
	Obj_Delete(obj);
}