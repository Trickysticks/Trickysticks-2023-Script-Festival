#TouhouDanmakufu
#ScriptVersion[3]
#System["./../tricky_system/TrickySystem.dnh"]
#Title["Cole Spell 3"]
#Background["./NamelessPath.dnh"]
#Player["./../lib/TrickyPlayer/Tricky.dnh"]
//#BGM["./../sfx/bgm/bgm2.ogg"]

#include "./../lib/TerraShot1x/shot_const.dnh"
#include "./../lib/includes/BossIncludes.dnh"

@Event{
	alternative(GetEventType())
		case(EV_REQUEST_LIFE){ SetScriptResult(1000); }
		case(EV_REQUEST_TIMER){ SetScriptResult(999); }
		case(EV_REQUEST_SPELL_SCORE){ SetScriptResult(10000000); }
		case(EV_GAIN_SPELL){ AddScore(ObjEnemyBossScene_GetInfo(objScene, INFO_SPELL_SCORE)); }
}

@Initialize{

	objEnemy = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objEnemy);
	Invuln(objEnemy, 360, 10, 15);
	PlayEnemyHitSound(objEnemy, 1);
	
let Cole = GetCurrentScriptDirectory~"sprites/Cole_Sheet.png";
	ObjPrim_SetTexture(objEnemy, Cole); 
	ObjSprite2D_SetSourceRect(objEnemy, 0, 0, 128, 128);
	ascent(x in 0..4){ ObjAnim_AddFrameA1(objEnemy, ANIM_IDLE, 0, 128*x, 128, 128*x+128);}
	descent(x in 0..4){ ObjAnim_AddFrameA1(objEnemy, ANIM_IDLE, 0, 128*x, 128, 128*x+128);}
	ascent(x in 0..4){ObjAnim_AddFrameA1(objEnemy, ANIM_MOVE_RIGHT, 128, 128*x, 128*2, 128*x+128);}
	ObjAnim_AddFrameA1(objEnemy, ANIM_MOVEEND_RIGHT, 128, 128*3, 128*2, 128*4);
	ObjAnim_AddFrameA1(objEnemy, ANIM_MOVEEND_RIGHT, 128, 128*2, 128*2, 128*3);
	ObjAnim_AddFrameA1(objEnemy, ANIM_MOVEEND_RIGHT, 128, 128*1, 128*2, 128*2);
	ascent(x in 0..4){ObjAnim_AddFrameA1(objEnemy, ANIM_MOVE_LEFT, 128*2, 128*x, 128*3, 128*x+128);}
	ObjAnim_AddFrameA1(objEnemy, ANIM_MOVEEND_LEFT, 128*2, 128*3, 128*3, 128*4);
	ObjAnim_AddFrameA1(objEnemy, ANIM_MOVEEND_LEFT, 128*2, 128*2, 128*3, 128*3);
	ObjAnim_AddFrameA1(objEnemy, ANIM_MOVEEND_LEFT, 128*2, 128*1, 128*3, 128*2);
	ascent(x in 0..3){ObjAnim_AddFrameA1(objEnemy, ANIM_ATTACK, 128*4, 128*x, 128*5, 128*x+128);}
	descent(x in 0..3){ObjAnim_AddFrameA1(objEnemy, ANIM_ATTACKEND, 128*4, 128*x, 128*5, 128*x+128);}

	ObjAnim_SetSpeedA1(objEnemy, ANIM_IDLE, 8);
	ObjAnim_SetSpeedA1(objEnemy, ANIM_MOVE_LEFT, 6);
	ObjAnim_SetSpeedA1(objEnemy, ANIM_MOVE_RIGHT, 6);
	ObjAnim_SetSpeedA1(objEnemy, ANIM_ATTACK, 5);
	ObjAnim_SetLoopPointA1(objEnemy, ANIM_MOVE_RIGHT, 3);
	ObjAnim_SetLoopPointA1(objEnemy, ANIM_MOVE_LEFT, 3);
	ObjAnim_SetLoopPointA1(objEnemy, ANIM_ATTACK, 2);
	ObjAnim_StartAnimationA2(objEnemy, 0, 0);
	ObjSprite2D_SetDestCenter(objEnemy); 
	ObjRender_SetScaleXYZ(objEnemy, 0.75, 0.75, 1);
	
	SetAutoDeleteObject(true);
	delayedstart;
	End;
}

task End {
	let itemx = 0;
	let itemy = 0;
	while(ObjEnemy_GetInfo(objEnemy, INFO_LIFE) > 0) { yield; }
	
	SetShotAutoDeleteClip(32, 40, 32, 32);
	NotifyEventAll(EV_SPELLBGEND, 1);
	//DeleteShotWave(1,GetX,GetY,20);
	DeleteShotAll(TYPE_ALL,TYPE_ITEM);
	PlaySoundEffect("end",60,GetCenterX);
	CreateItems02(12, ITEM_POINT_S, GetEnemyX, GetEnemyY, 0, -50, 35, 35, 10000);
	SetForbidPlayerShot(false);
	SetPlayerSpeed(4.2, 1.7);
	SetCommonData("charm", false);
	Obj_Delete(objEnemy);
	CloseScript(GetOwnScriptID);
}

task delayedstart{
	
	ObjMove_SetDestAtFrame(objEnemy, GetCenterX, 120, 60);
	wait(60);
	
	ObjEnemyBossScene_StartSpell(objScene);
	ObjEnemyBossScene_SetSpellTimer(objScene, 60*(60));
	PlaySoundEffect("cardcall", 90, GetCenterX);
	
	let cole_cut = GetCurrentScriptDirectory~"sprites/Cole_Cutin.png";
	ObjCutin_SetSpellcardS4("Horticulture &quot;Growing the Seed of Evil&quot;", cole_cut, "NAZRIN", "124", "252", "0");
	ObjCutin_LaunchS3("NAZRIN", "0", "Hard");
	NotifyEventAll(EV_SPELLBG, 1);
	
	wait(120);
	attack1;
	//attack2;
	SetShotAutoDeleteClip(32, 40, 32, 16);
}

task attack1{
	let xLoc = 0;
	let yLoc = 0;
	let x = -20;
	let repeats = 8;
	let offset = 0;
	let one = 1;
	let count = 0;
	let startPoint = [-20, 404];
	angle = 90;
	TCharge01(GetEnemyX, GetEnemyY, 3, 45, 124, 252, 0, BLEND_ADD_ARGB, 25);
	PlaySoundEffect("charge2", 100, GetEnemyX);
	wait(45);
	
	loop{
		ascent(i in 0..60){
				if(i%2==0){ PlaySoundEffect("shot2", 75, GetCenterX); }
			loop(5){
				xLoc = GetEnemyX+(25+(i*6))*cos(angle);
				yLoc = GetEnemyY+(25+(i*6))*sin(angle);
				if(GetDistance(xLoc, yLoc, GetPlayerX, GetPlayerY)>32){
					let obj = CreateDNHShotA1(xLoc, yLoc, 0, angle, [SHOT_BALL_AURA_PURPLE, SHOT_BALL_AURA_PURPLE, SHOT_BALL_AURA_GREEN][color%3], 10);
					if(color%3==2){ GrowFlower(obj, (angle%360)^2); } else { DelayedAcceleration(obj, i); }
					PreventSpawnkill(obj);
				}
				angle+=360/5;
			}
		angle+=8.75*one;
		color++;
		wait(3);
		}
		one*=-1;
		wait(90);
		ExMove(objEnemy, GetCenterX, rand(15, 20), 120+rand(-5, 5), 20, 3, 15);
		wait(90);
		
	}	
}
	
	task DelayedAcceleration(obj, i){
		wait(60+(i/2));
		if(!Obj_IsDeleted(obj)){
			ObjMove_SetAcceleration(obj, 0.02);
			ObjMove_SetMaxSpeed(obj, 3.5);
		}
	}

	task GrowFlower(obj, ang){
		let scale = 1;
		wait(90);
		let dir = ang;
		if(!Obj_IsDeleted(obj)){
			PlaySoundEffect("shot5", 60, ObjMove_GetX(obj));
			loop(5){
				let obj2 = CreateDNHShotA1(ObjMove_GetX(obj)+10*cos(dir), ObjMove_GetY(obj)+10*sin(dir), 0, dir, SHOT_SAKURA_RED, 30);
				ManagePetals(obj2);
				dir+=360/5;
			}
		}
		loop(30){
			if(!Obj_IsDeleted(obj)){
				scale-=1/30;
				ObjRender_SetScaleXYZ(obj, scale);
				ObjShot_SetIntersectionScaleXY(obj, scale, scale);
			}	
			yield; 
		}
	}
	
	task ManagePetals(obj){
		wait(60);
		if(!Obj_IsDeleted(obj)){
			ObjMove_SetAcceleration(obj, 0.01);
			ObjMove_SetMaxSpeed(obj, 2);
		}
	}
	
	task PreventSpawnkill(obj){
		loop(5){
			if(!Obj_IsDeleted(obj)){
				if(GetDistance(ObjMove_GetX(obj), ObjMove_GetY(obj), GetPlayerX, GetPlayerY)<31){
					Obj_Delete(obj);
				}
			}
		yield;
		}
	}